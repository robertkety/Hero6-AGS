/* Room Variables */
bool startedSneaking = false;

/* Room Functions */
function EndScene() {
  cGuard03.Walk(195, 128, eNoBlock, eWalkableAreas);
  cGuard02.Walk(195, 128, eBlock, eWalkableAreas);
  StopMusic();
  cGuard03.ChangeRoom(-1);
  cGuard02.ChangeRoom(-1);
  cRoguep.ChangeRoom(15,305,166);
  cRoguep.UnlockView();
  cRoguep.Loop = 0;
  gNoisemeter.Visible = false;
}

function AngryGuards() {
  cGuard03.Walk(101, 145, eBlock, eWalkableAreas);
  cGuard03.Say("Are you okay my friend?");
  cGuard02.UnlockView();
  cGuard02.Say("Just a scratch.");
  
  cGuard03.Loop = 0;
  Wait(10);
  Display("The guard turns to you.");
  cGuard03.Say("Looks like we beat them. No thanks to you.");
  cGuard03.Say("You one of those pathetic hero wannabes? We are the heroes round here. So why don't you GET LOST!");
  flags.IntroFight = 2;
}

function HappyGuards() {
  cGuard03.Walk(101, 145, eBlock, eWalkableAreas);
  cGuard03.Say("Are you okay my friend?");
  cGuard02.UnlockView();
  cGuard02.Say("Just a scratch.");
  cGuard03.Walk(cGuard03.x,  cGuard03.y + 20, eBlock, eWalkableAreas);
  cGuard03.Say("Thanks. I guess we owe you one.");
  cGuard02.Say("Although lets be honest. If it hadn't been for us, the rogue would have been too strong for you.");
  cGuard03.Say("You look like one of them Hero types. You best watch your step. I hear we've had five of them go missing already.");
  flags.IntroFight = 2;
}

function StartCombat() {
  cRoguemonster.Walk(cEgo.x + 15, cEgo.y - 8, eBlock, eWalkableAreas);
  RegisterEnemy(cRoguemonster, 4);
  SpawnEnemyCoords(4, cRoguemonster.x, cRoguemonster.y);
  cRoguemonster.ChangeRoom(-1);
  setCombatDeathMode(false);
  setMovementMode(false);
  flags.IntroFight = 1;

  DecreaseMonsterHealth(cEnemy1, 20);
  setAggressiveness(-50);
  gNoisemeter.Visible = false;
  alertLevel = 0.0;
  InitCombat();
}

function RogueLeaves() {
  cRoguemonster.Walk(cRoguemonster.x, 305, eBlock, eAnywhere);
  cRoguemonster.ChangeRoom(-1);
}

function HandleDaggerActions(){
  if (sneakingMode) {
    if (cEgo.x < cRoguemonster.x + 20 && cEgo.y < cRoguemonster.y + 10) {
      StopMusic();
      cEgo.LockView(HERO_SLASH);
      cEgo.Animate(1, 2, eOnce, eBlock, eForwards);
      cEgo.UnlockView();
      gNoisemeter.Visible = false;
      Display("Your sneaky attack from behind has caught the rogue off guard and forced him to retreat.");
      cRoguemonster.Say("You ain't seen the last of us!!");
      RogueLeaves();
      HappyGuards();
      SetSneakingMode(0);
      return;
    }
    else {
      Display("If you intend to perform a surprise melee attack on the rogue, you'll need to get closer.");
      return;
    }
  }
  
  if (cFlyingDagger.Room == 9) return;
  
  cEgo.LoseInventory(iDagger);
  cEgo.LockView(HERO_THROWDAG);
  int loo = 0;
  if (cEgo.x < cRoguemonster.x) loo = 2;
  else loo = 1;
  
  cRoguemonster.FaceCharacter(cEgo);
  cEgo.Animate(loo, 2, eOnce, eNoBlock, eForwards);
  while (cEgo.Frame < 7) Wait(1);
  //cEgo.FaceCharacter(cRoguemonster); not sure what the point of this line is
  cFlyingDagger.ChangeRoom(9, cEgo.x, cEgo.y);
  cFlyingDagger.z = 40;
  cFlyingDagger.Walk(cRoguemonster.x, cRoguemonster.y, eNoBlock, eAnywhere);
  cEgo.UnlockView();
}

function RogueSeesHero() {
  if (cRoguemonster.Room != 9) return;
  
  cRoguemonster.FaceCharacter(cEgo);
  cEgo.StopMoving();
  cRoguemonster.Say("Hey. Who's that!");
  Display("The hostile rogue has seen you. You prepare for battle.");
  StartCombat();
}

function hitRogueWithSpell() {
  if (cRoguemonster.Room != 9) return;
  cFlame.ChangeRoom(-1);
  cRoguemonster.FaceCharacter(cEgo);
  cEgo.StopMoving();
  cRoguemonster.LockView(ROGUE_COMBAT);
  cRoguemonster.Animate(2, 3, eOnce, eBlock, eForwards);
  cRoguemonster.Say("Ouch! Now I'm mad!");
  Display("The hostile rogue has seen you. You prepare for battle.");
  cRoguemonster.UnlockView();
  cRoguemonster.Walk(cEgo.x + 15, cEgo.y - 8, eBlock, eWalkableAreas);
  RegisterEnemy(cRoguemonster, 4);
  SpawnEnemyCoords(4, cRoguemonster.x, cRoguemonster.y);
  cRoguemonster.ChangeRoom(-1);
  setCombatDeathMode(false);
  setMovementMode(false);
  flags.IntroFight = 1;

  DecreaseMonsterHealth(cEnemy1, 30);
  setAggressiveness(-50);
  InitCombat();  
}

function hitRogueWithDagger() {
  if (cRoguemonster.Room != 9) return;
  short goodHit = heroinfo.throw + ((Random(heroinfo.luck) * 2) / 3) + (heroinfo.agi / 3);
  if (goodHit >= 49) {
    StopMusic();
    Display("Your dagger hits the rogue solidly. He is obviously hurt.");
    cRoguemonster.FaceCharacter(cEgo);
    cRoguemonster.Say("You'll never get away with that!");
    cRoguemonster.Say("You have not seen the last of us!!");
    RogueLeaves();
    HappyGuards();
    EndScene();
  }
  else {
    Display("The rogue has successfully evaded your dagger with little more than a scratch.");
    cRoguemonster.Say("Thought that was clever eh? Prepare to die!");
    StartCombat();
  }  
}

function HeroLoseFight() {
  flags.IntroFight = 2;
	cRoguemonster.ChangeRoom(9, cEnemy1.x, cEnemy1.y);
	setAggressiveness(0);
	cEnemy1.ChangeRoom(-1);
	setCombatDeathMode(true);
	cEgo.LockView(HERO_COLLAPSE);
	cEgo.Animate(0, 3, eOnce, eBlock, eForwards);
	cRoguemonster.Say("They are all the same.");
 	Display("Your first battle against the rogues prove pitiful as you collapse to the ground in defeat. What an unfortunate way to begin your first adventure.");

	cRoguemonster.Loop = 3;
	cRoguemonster.Say("You haven't seen the last of us, you fools.");
	RogueLeaves();
  cGuard03.Walk(101, 145, eBlock, eWalkableAreas);
  cGuard03.Say("Are you okay my friend?");
  cGuard02.UnlockView();
  cGuard02.Say("Just a scratch.");
  cGuard03.Loop = 0;
  cGuard03.Say("Oh no. Its another one of them hero types.");
  cGuard03.Say("Hey guys! Bring one of Belenus' remedies. This sucker's taken a bit of a blow.");
  EndScene();
  cEgo.LockView(HERO_GETUP);
  cEgo.UnlockView();
}
	
function HeroWinFight() {
  flags.IntroFight = 2;
	cRoguemonster.ChangeRoom(9, cEnemy1.x, cEnemy1.y);
	cEnemy1.ChangeRoom(-1);
	setAggressiveness(0);
	setCombatDeathMode(true);
	GiveScore(5);
  Display("You win.");  
	Display("The wounded rogue retreats upon his defeat in battle.");
	cRoguemonster.Say("Beware! You have made a powerful enemy today. Make the most of your victory while you still can. ");
	cRoguemonster.Loop = 3;
	cRoguemonster.Say("You have not seen the last of us!!");
	RogueLeaves();
  HappyGuards();
  EndScene();
}

function GuardThreatenRogue() {
  cGuard03.Walk(cRoguep.x - 9,cRoguep.y,eBlock,eWalkableAreas);
  cGuard03.Loop = 2;
  cGuard03.Say("You! Get out of here or I'll kill him!");
  cRoguemonster.Say("You wouldn't dare.");
  Display("The enraged guard points his sword only a few inches from the fallen rogue's throat.");
  cGuard03.Say("This is your last warning.");
  cRoguemonster.Say("Pah! I remember when even you had honor, you coward!");
  cRoguemonster.Say("You haven't seen the last of us!");
}

function HeroRetreats() {
  StartCutscene(eSkipESCOnly);
  Display("Having decided you are not ready for this kind of confrontation, you flee the scene and hope the situation resolves itself.");
  GuardThreatenRogue();
  StopMusic();
  RogueLeaves();
  cGuard03.ChangeRoom(-1);
  cRoguep.ChangeRoom(15,305,166);
  cRoguep.UnlockView();
  cRoguep.Loop = 0;
  EndCutscene();
}

function HeroDoNothing() {
  if (flags.IntroFight >= 1 || cRoguemonster.Room != 9) return;
  StartCutscene(eSkipESCOnly);
  GuardThreatenRogue();
  StopMusic();
  RogueLeaves();
  AngryGuards();
  EndCutscene();
}

/* Events */
function postFade() {  // script for Room: Player enters room (after fadein)
  //Need to reset transparency because Room100 title page sets it to 99
  cEgo.Transparency = 0;
  
  if (cEgo.PreviousRoom == 152 || cEgo.PreviousRoom == 101) {
    //SetScreenTransition(eTransitionFade);
    cEgo.Walk(cEgo.x,230,eBlock,eAnywhere);
  }

  if (flags.Chapter == 0) {
    StartCutscene(eSkipESCOnly);  
    mouse.Mode = eModeWalkto;
    PlayMusic(14);
    flags.Chapter = 1;
    Display("As you approach the Albion gate, you find it is under siege!");
    cRoguep.LockView(ROGUE_COMBAT);
    cRoguep.Animate(0, 3, eOnce, eBlock, eForwards);
    cGuard02.Animate(0, 2, eOnce, eBlock, eForwards);
    cRoguep.Animate(0, 3, eOnce, eBlock, eForwards);
    cGuard02.Animate(0, 2, eOnce, eBlock, eForwards);
    cRoguep.LockView(ROGUE_DEATH);
    cRoguep.Animate(0, 3, eOnce, eBlock, eForwards);
    cRoguep.Solid = false;
    Display("You watch as the rogue falls under the might of the city guard. But even he can not hide his apprehension.");
    
    cGuard02.Say("Help! I can't hold them much longer. Close the gate!");
    cGuard03.Say("Oh no. It's stuck again!");
    cGuard02.Say("HELP!");
    
    cRoguemonster.LockView(ROGUE_THROWDAG);
    cRoguemonster.Animate(0, 2, eOnce, eNoBlock, eForwards);
    while(cRoguemonster.Frame < 6) Wait(1);
    oDagger.X = cRoguemonster.x;
    oDagger.Y = cRoguemonster.y - 40;
    
    oDagger.Visible = true;
    oDagger.Move(cGuard02.x, cGuard02.y - 35, 4, eBlock, eAnywhere);
    cRoguemonster.UnlockView();
    oDagger.Visible = false;
    Display("TODO: Guard collapses");
    
    cGuard03.Say("I've fixed the gate. C'mon! Hurry!");
    cRoguemonster.Say("Ha! Too late! Your friend is close to death.");
    cRoguemonster.Say("Anyone else?");
    EndCutscene();
    SetTimer(11, 400);
    return;
  }
  if (flags.Chapter == 1 && flags.DukeRescue == 6) {
    Display("You hear excited shouts from the guards.");
    cGuard02.Say("By the gods! He's carrying the Duke!");
    cBelenus.ChangeRoom(9, 132, 134);
    Display("Your heroics have attracted a large crowd. Belenus rushes forward, examining the Duke.");
    cBelenus.Say("Oh dear! I'm afraid you shouldn't have moved him. All this movement is causing his poison to accelerate!");
    cBelenus.Say("Guards! Help me carry him to my hut. He needs to be treated immediately!");
    Display("You ask Belenus if the Duke can be cured. Belenus bows his head.");
    cBelenus.Say("Unless we find the miracle herb, Lauburu, he can't be cured.");
    cBelenus.ChangeRoom(34, 250, 170);
  
    flags.DukeRescue = 7;
    heroinfo.gold += 25;
    flags.Chapter = 2;
    flags.MobOccuring = 1;
    Wait(40);
    Display("Congraulations! You have completed the Find the Duke Quest. Please report any bugs, weirdness or quirks you spotted in the progress of his quest");
    Display("Begin Chapter 2");
		//cEgo.ChangeRoom(11, 130, 133);
  }  

  if (flags.Chapter == 2 && flags.DolmenQuest > 0 && flags.AngusNote == 0 && !flags.VisitedDarkForest && flags.GreenleafFavor == 0 /*&& isday()*/) {
    flags.AngusNote = 1;
    cAngus.Say("Hey you! Some lady wanted me to give you this.");
    cEgo.Walk(226, 192, eBlock, eWalkableAreas);
    Display("Angus hands you a note. You start to reads it.");
    DisplayMessage(539);
    flags.ShadowMageApproaching = 1;
    cEgo.AddInventory(iGNote);
  }
}

function preFade() {  // script for Room: Player enters room (before fadein)
  cAngus.Loop = 1;
  if (isDaytime()) cAngus.ChangeRoom(9);
  else cAngus.ChangeRoom(-1);

  StopMusic();

  if (flags.Chapter == 0) {
    cGuard03.ChangeRoom(9, 195, 128);
    cGuard02.LockView(GUARD_ATTACK);
    cRoguep.ChangeRoom(9, 114, 160);
    cRoguep.Loop = 7;
    cRoguemonster.ChangeRoom(9,  176, 180);
    cRoguemonster.Loop = 7;
  }
}

function firstEntry(){
}

function onExit(){
}

function repExec() {  // script for Room: Repeatedly execute
  if (IsTimerExpired(11) && flags.IntroFight == 0) {
    cEgo.StopMoving();
    cEgo.FaceLocation(cEgo.x, 0, eBlock);
    HeroDoNothing();
  }

  if (isHeroInCombat() == false && isHeroDefeated() == true && flags.IntroFight == 1) {
    HeroLoseFight();
  }
  else if (isHeroInCombat() == false && isHeroDefeated() == false && flags.IntroFight == 1) {
    HeroWinFight();
  }

  if (Character.GetAtScreenXY(cFlame.x, cFlame.y) == cRoguemonster && flags.IntroFight == 0) {
    hitRogueWithSpell();
  }

  if (cFlyingDagger.Room == 9) {
    while (cFlyingDagger.Moving) Wait(1);
    cFlyingDagger.ChangeRoom(-1);
    cEgo.UnlockView();
    hitRogueWithDagger();
  }
  if (GetGlobalInt(1)==1) {
    SetGlobalInt(1, 0);
    HandleDaggerActions();
  }
  
  if (sneakingMode && !startedSneaking) {
    startedSneaking = true;
    gNoisemeter.Visible = true;
    SetTimer(11, 200);
  }

  if (!sneakingMode && startedSneaking) {
    gNoisemeter.Visible = false;
    alertLevel = 0.0;
    HeroDoNothing();
  }
  
  if (startedSneaking) {
    regionNoise = 1.0;
    if (cEgo.Moving) {
      //alertLevel += 0.3;
      float x = (IntToFloat(100 - heroinfo.stealth) / 50.0) * regionNoise;
      alertLevel += x;
      btnAlertLevelMeter.Value = floor(alertLevel);
      if (alertLevel > 99.0) {
        RogueSeesHero();
        return;
      }
    }
    else {
      alertLevel -= 0.1;
      btnAlertLevelMeter.Value = floor(alertLevel);
    }
  }
}

function exitSouth(){
  cEgo.Walk(cEgo.x, 297,eBlock,eAnywhere);
  if (cRoguemonster.Room == 9) {
    HeroRetreats();
  }
  
  player.ChangeRoom(152, 240, 220);
}

function exitWest(){
  cEgo.Walk(-20, cEgo.y,eBlock,eAnywhere);
  if (cRoguemonster.Room == 9) {
    HeroRetreats();
    flags.IntroFight = 2;
  }

  player.ChangeRoomAutoPosition(12);
}

function exitEast(){
}

function exitNorth() {  // script for Room: Walk off top screen edge
  cEgo.ChangeRoom(2,180,290);
}

/* Hotspots */
function hIvy_Look(){
  DisplayMessage(0);
}

function hIvy_Interact(){
  DisplayMessage(1);
}

function hIvy_Talk(){
  DisplayMessage(2);
}

function hCityWall_Look(){
  DisplayMessage(15);
}

function hCityWall_Interact(){
  DisplayMessage(16);
}

function hCityWall_Talk(){
  DisplayMessage(17);
}

function hHay_Look(){
  DisplayMessage(12);
}

function hHay_Interact(){
  DisplayMessage(13);
}

function hHay_Talk(){
  DisplayMessage(14);
}

function hBarn_Look(){
  DisplayMessage(7);
}

function hBarn_Interact(){
  DisplayMessage(8);
}

function hBarn_Talk(){
  DisplayMessage(9);
}

function hArchRelief_Look(){
  DisplayMessage(19);
}

function hTownSquare_Look(){
  DisplayMessage(18);
}

function hCart_Look(){
  DisplayMessage(10);
}

function hCart_Interact(){
  DisplayMessage(11);
}

function hFence_Look(){
  DisplayMessage(20);
}

function hPath_Look() {   // script for Hotspot 12 (Hotspot 12): Look at hotspot
  dirtground_look(); 
}

function hPath_Interact() {  // script for Hotspot 12 (Hotspot 12): Interact hotspot
  dirtground_use();
}

function hGrass_Look() {  // script for Hotspot 10 (Hotspot 10): Look at hotspot
  grass_look();  
}

function hGrass_Interact() {  // script for Hotspot 10 (Hotspot 10): Interact hotspot
  grass_use();
}

function hGrass_Talk() {  // script for Hotspot 10 (Hotspot 10): Talk to hotspot
  grass_talk();  
}

function hTree_Look() {  // script for Hotspot 7 (Hotspot 7): Look at hotspot
  tree_look();  
}

function hTree_Interact() {  // script for Hotspot 7 (Hotspot 7): Interact hotspot
  tree_use();  
}

function hTree_Talk() {  // script for Hotspot 7 (Hotspot 7): Talk to hotspot
  tree_talk();  
}

function hSky_Look() {  // script for Hotspot 5 (Hotspot 5): Look at hotspot
  sky_look();  
}

function hSky_Interact() {  // script for Hotspot 5 (Hotspot 5): Interact hotspot
  sky_use();  
}

function hSky_talk() {  // script for Hotspot 5 (Hotspot 5): Talk to hotspot
  sky_talk();
}

/* Regions */
function rShadows() {  // script for Region 3: Player walks onto region
  if (!sneakingMode) RogueSeesHero();
}

function rStepOnLine() {  // script for Region 4: Player walks onto region
  if (!sneakingMode) RogueSeesHero();
}
