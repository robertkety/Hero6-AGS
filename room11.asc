bool notyet;
bool closetodoor = false;

function postfade() {  // script for Room: Player enters room (after fadein)
  closetodoor = false;
  
  notyet = true;
  if (cEgo.PreviousRoom == 40) {
    cEgo.Walk(cEgo.x, 133, eBlock, eAnywhere);
  }
  notyet = false;  
}

function preFade(){
  aMusic10.Play();
}

function firstEntry(){
}

function onExit(){
}

function repExec() {  // script for Room: Repeatedly execute
  // handle guard patrolling
  short gux = cGuard01.x;
  short guy = cGuard01.y;

  if (gux <= 125) {
    short ret = Random(500);
    if (ret < 2) {
      cGuard01.Walk(155, guy);
    }
    else {
      //cGuard01.Loop = 0;
    }
  }
  else if (gux >= 150) {
    short ret = Random(500);
    if (ret < 2) cGuard01.Walk(120, guy); 
    //else cGuard01.Loop = 0;
  } 
}

function exitSouth(){
  player.ChangeRoomAutoPosition(6);
}

function exitWest(){
}

function exitEast(){
  player.ChangeRoomAutoPosition(7);
}

function exitNorth(){
}

/* Objects */

/* Hotspots */
function hTree_Look() {  // script for Hotspot 4 (Hotspot 4): Look at hotspot
  tree_look();
}

function hCastleGate_Interact() {  // script for Hotspot 7 (Hotspot 7): Interact hotspot
  if (closetodoor) {
    cEgo.ChangeRoom(40,84,208);
    closetodoor = false;
  }
  else DisplayMessage(0);
}

function hBlacksmithDoor_Interact() {  // script for Hotspot 1 (Hotspot 1): Interact hotspot
  if (isDaytime()) {
    cEgo.Walk(68, 206, eBlock, eWalkableAreas);
    cEgo.Loop = 3;
    cEgo.ChangeRoom(16, 165, 300);
  }
  else DisplayMessage(1);
}

function hSky_Look() {  // script for Hotspot 9 (Hotspot 9): Look at hotspot
  sky_look();  
}

function hSky_Interact() {  // script for Hotspot 9 (Hotspot 9): Interact hotspot
  sky_use();
}

/* Regions */
function rCastleGate_WalkOff() {  // script for Region 2: Player walks off region
  closetodoor = false;  
}

function rCastleGate_WalkOn() {  // script for Region 2: Player walks onto region
  
  closetodoor = true;
  
  if (notyet == true) return;
  
  if (flags.DukeRescue < 3 && cGuard01.Room == 11 && flags.Chapter == 1) {
    cEgo.StopMoving();
    cGuard01.StopMoving();
    if (IsCharacterFacing(cGuard01,  cEgo)) {
			cGuard01.FaceCharacter(cEgo);
			cGuard01.Say("Halt. Nobody enters the castle unless it's for important business.");
			cGuard01.Say("This means you! BEAT IT!");
			cEgo.Walk(cEgo.x,  cEgo.y + 10, eBlock, eWalkableAreas);
			closetodoor = false;
		}
		else {
			cGuard01.FaceCharacter(cEgo);
			cGuard01.Say("Halt. Nobody enters the castle unless it's for important business.");
			cGuard01.Say("Don't dare think that you can sneak past behind me. I'm watching you!");
			cEgo.Walk(cEgo.x,  cEgo.y + 10, eBlock, eWalkableAreas);
			closetodoor = false;
		}
		  
  }
  else if (flags.Chapter == 2 && cGuard01.Room == 11) {
    cEgo.StopMoving();
    cGuard01.StopMoving();
    cGuard01.FaceCharacter(cEgo);
    cGuard01.Say("The Duchess will not be disturbed right now. You may not enter.");
    cEgo.Walk(cEgo.x,  cEgo.y + 10, eBlock, eWalkableAreas);
    closetodoor = false;
  }
    
}