/* Cutscenes */
function LeaveRoom() {
  cEgo.Walk(-10, cEgo.y, eBlock, eAnywhere);
  cFernin.LockView(FERNIN_APPEAR);
  cFernin.Animate(0, 2, eOnce, eBlock, eBackwards); 
  cEgo.ChangeRoom(4, 104, 136);
}  

function FerninAppear() {
  cFernin.ChangeRoom(19);
  cFernin.LockView(FERNIN_APPEAR);
  cFernin.Animate(0, 2, eOnce, eBlock, eForwards);  
}

function TellHeroAboutCape() {
	cFernin.Say("Here's what I've discovered so far. That cape was worn by a mage who died more than twenty years ago.");
	cFernin.Say("I suspect, though I can't be sure, that these shadow mages are some kind of silhouettes of deceased men.");
	cFernin.Say("I'm afraid that's all I can tell you for now. Marvin, however, should be able to shed more light on this mystery.");
	cFernin.Say("I'd contact him myself via that crystal ball you see over there, but lately I've not been able to reach him at all.");
	cFernin.Say("The only other way to reach him is to enter his tower, which can only be accessed through the caves in the dark forest.");
	cFernin.Say("Unfortunately, I have not been able to enter the dark forest for some time due to reasons unknown.");
	cFernin.Say("You, however, managed to penetrate its defenses, and so you're more suited to enter Marvin's tower to find him.");
	cFernin.Say("I've written my findings on this scroll. Please hand it to him. He'll know what to do.");
	cEgo.AddInventory(iFScroll);
	cFernin.Say("Any questions? TODO");
	flags.TowerQuest = 1;
}

/* Events */
function postFade() {  // script for Room: Player enters room (after fadein)
  if (flags.Chapter == 2 && flags.DlgTethra == 3) {
    Display("Meanwhile....");
    PlayMusic(18);
    cFernin.Say("Hand me another piece of that cape.");
    cFlasamus.Say("Yes master!");
    cFernin.Say("I told you to stop calling me that!");
    cFlasamus.LockView(FLASAMUS_LOWER);
    cFlasamus.Animate(0, 3, eOnce, eBlock, eForwards);
    oCape.Visible = true;
    oCape.X = cFlasamus.x;
    oCape.Y = cFlasamus.y;
    oCape.Move(oCape.X, 151, 4, eBlock, eAnywhere);
    Wait(20);
    cFernin.Say("You numbskull.");
    cFlasamus.Say("Hey. Look at the cauldron! Somethings happening!");
    Wait(1);
    PlaySound(12);
    PlayVideo("cauldron.avi", 0, 11);
    
    cFernin.Say("Interesting...");
    cFlasamus.Say("I'll say.");
    Wait(40);
    cEgo.Transparency = 0;
    
    flags.DlgTethra = 4;
    flags.Chapter = 3;
    cEgo.Loop = 0;
    cEgo.ChangeRoom(44, 64, 176);
  }
  else {
    PlayMusic(13);
  }
  if (cEgo.PreviousRoom == 4) cEgo.Walk(12, cEgo.y, eBlock, eAnywhere);
  
  if (timeinfo.Day >= flags.DayTheMagicShopReopens) {
    FerninAppear();
    flags.DisturbedFernin = 0;
    flags.DayTheMagicShopReopens = 32767;
    cFernin.Say("Ah. You've returned. Please come closer.");
    cEgo.Walk(180, 215,eBlock,eWalkableAreas);
    TellHeroAboutCape();
  }
  if (flags.DisturbedFernin == 1) {
    FerninAppear();
    cFernin.Say("I mustn't be disturbed right now. Come back tomorrow and I'll tell you my findings.");
    //cFernin.Say("I will see you tomorrow. Good day.");
    flags.DisturbedFernin = 2;
    LeaveRoom();
  }
  else if (flags.DisturbedFernin == 2) {
    FerninAppear();
    cFernin.Say("You again! I've told you not to disturb me at this time. I cannot analyze this cape and guard the shop at the same time.");
    cFlasamus.Say("Maybe you should lock the door.");
    cFernin.Say("I would if somebody hadn't just lost the key.");
  
    flags.DisturbedFernin = 3;
    LeaveRoom();
  }
  else if (flags.DisturbedFernin == 3) {
    FerninAppear();
    cFernin.Say("What is this, child's play? I have asked you not to disturb me enough times. Come back tomorrow and I will give you a free ear test perhaps.");
    flags.DisturbedFernin = 4;
    LeaveRoom();
  }
  else if (flags.DisturbedFernin == 4) {
    FerninAppear();
    cFernin.Say("YOU AGAIN!! Would you like me to demonstrate what happens when wizards are bugged for too long?");
    Display("You'd better leave. You wouldn't like him when he's angry.");
    flags.DisturbedFernin = 5;
    LeaveRoom();
  }
  else if (flags.DisturbedFernin == 5) {
    cEgo.Walk(50, cEgo.y, eBlock, eWalkableAreas);
    FerninAppear();
    cFernin.Say("Ok. You asked for it!");
    // TODO_ Transform
    cFernin.LockView(FERNIN_IDLE);
    cFernin.Animate(0, 2, eOnce, eBlock, eForwards);
    cFernin.UnlockView();
    cFernin.Loop = 0;
    cEgo.LockView(DUCK);
    Wait(40);
  
    Display("The enraged Fernin appears to have transformed you into a duck.");
    cFernin.Say("That'll teach you.");
    cFlasamus.Say("Looks like you'll need a quack doctor to fix you now.");
    cFernin.Say("Yeah, you'll need a.....what?");
  
    Wait(4);
    cFilter.ChangeRoom(cEgo.Room);
    cFilter.Transparency = 100;//TODO --- Check that the value gives the desired effect
    Display("You are now forced to spend the night in a rusty old cage in Fernin's domain. At least, you got to see Fernin trying to analyze the cape.");
    Display("The next morning, Fernin transforms you back. You quickly check yourself to see if you're truly back in one piece. Thankfully, Fernin didn't leave out anything.");
    cFilter.ChangeRoom(cEgo.Room);
    cFilter.Transparency = 0;//TODO --- Check that the value gives the desired effect
    cEgo.LockView(GetModeView());
    Wait(4);
    timeinfo.Day++;
    timeinfo.ticks = 3000;
    TellHeroAboutCape();
    
    flags.DayTheMagicShopReopens = 32767;
    flags.DisturbedFernin = 6;
    //LeaveRoom();
  }
}

function preFade() {  // script for Room: Player enters room (before fadein)
  if (flags.Chapter == 2 && flags.DlgTethra == 3) {
    cEgo.Transparency = 100;
    oCauldron.Visible = true;
  }
}

function firstEntry(){}

function onExit(){}

function repExec(){}

function exitSouth(){}

function exitWest(){
  player.ChangeRoom(4, 104, 136);
}

function exitEast(){}

function exitNorth(){}

/* Object */

/* Hotspots */

/* Regions */

