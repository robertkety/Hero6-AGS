// new module script

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function initSaveLoadGameDialog(bool isSaveGameDialog)
{
  PauseGame();
  mouse.UseModeGraphic(eModePointer);
  
  gIconbar.Visible = false;
  if(gOptions.Visible) gOptions.Visible = false;
  
  lstSaveGames.FillSaveGameList();
  lstSaveGames.SelectedIndex = 0;
  if(lstSaveGames.SelectedIndex != -1)
  {
    txtSaveName.Text = lstSaveGames.Items[lstSaveGames.SelectedIndex];
  }
  
  if(isSaveGameDialog)
  {
    lblSaveLoadDescription.Text = "Type the description of the saved game.";
    txtSaveName.Visible = true;
    btnSave.Visible = true;
    btnLoad.Visible = false;
  }
  else
  {
    lblSaveLoadDescription.Text = "Select the game you would like to load.";
    txtSaveName.Visible = false;
    btnSave.Visible = false;
    btnLoad.Visible = true;
  }
  gSaveLoadGame.Visible = true;
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function cancelSaveLoadGame_OnClick()
{
  if(cEgo.Room != 100)
  {
    mouse.reloadCursor();
    mouse.UseDefaultGraphic();
  }
  gIconbar.Visible = true;
  gSaveLoadGame.Visible = false;
  UnPauseGame();
}

/*
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function save_OnClick(GUIControl *control, MouseButton button)
{
  int totalSaves = lstSaveGames.ItemCount;
  String saveName = txtSaveName.Text;
  
  if(saveName == "")
  {
    Display("Please enter a name for your savegame");
    return;
  }
  
  if(lstSaveGames.SelectedIndex == -1)
  {
    SaveGameSlot(totalSaves+1, saveName);
    cancelSaveLoadGame_OnClick();
    return;
  }
  else
  {
    int checkSave = 0;
    while(checkSave != totalSaves)
    {
      if(saveName == lstSaveGames.Items[checkSave])
      {
        SaveGameSlot(savegameindex[checkSave], saveName);
        cancelSaveLoadGame_OnClick();
        return;
      }
      checkSave++;
    }
    SaveGameSlot(totalSaves+1, saveName);
    cancelSaveLoadGame_OnClick();
  }
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function load_OnClick(GUIControl *control, MouseButton button)
{
  int totalLoads = lstSaveGames.ItemCount;
 
  if(lstSaveGames.SelectedIndex == -1)
  {
    Display("Please select a slot to load.");
  }
  else if(lstSaveGames.SelectedIndex >= 0)
  {
    RestoreGameSlot(savegameindex[lstSaveGames.SelectedIndex]);
    cancelSaveLoadGame_OnClick();
  }
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function deleteSaveGame_OnClick(GUIControl *control, MouseButton button)
{
  if(lstSaveGames.SelectedIndex != -1)
  {
    DeleteSaveSlot(savegameindex[lstSaveGames.SelectedIndex]);
    lstSaveGames.FillSaveGameList();
  }
  else
  {
    Display("Please select a save slot to delete.");
  }
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function saveGames_OnSelectionChange(GUIControl *control)
{
  txtSaveName.Text = lstSaveGames.Items[lstSaveGames.SelectedIndex];
}

function game_start () {
  gSaveLoadGame.Visible = false;
  gSaveLoadGame.Centre();
}

function on_key_press(int keycode)
{  
  if (cEgo.Room != 100 && cEgo.Room != 101)
  {
    if(keycode==363)// F5 
    {
      if(gSaveLoadGame.Visible)
      {
        cancelSaveLoadGame_OnClick();
      }
      else
      {
        initSaveLoadGameDialog(true);
      }
    }
    if (keycode==365)// F7 
    {
      if(gSaveLoadGame.Visible)
      {
        cancelSaveLoadGame_OnClick();
      }
      else
      {
        initSaveLoadGameDialog(false);
      }
    }      
  }                
}

function on_mouse_click(MouseButton button)
{
  if(gSaveLoadGame.Visible)
  {
    if(button == eMouseWheelNorth)
    {
      lstSaveGames.ScrollUp();
    }
    else if(button == eMouseWheelSouth)
    {
      lstSaveGames.ScrollDown();
    }
  }
} 